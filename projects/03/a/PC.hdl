// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Can be built from a register
    // an incrementor, and some logic gates
    
    // Increment the "out" from the clock cycle
    Inc16(in=rout, out=incout);

    // Select between reset, load, inc
    // if no inc, carry rout to next mux
    Mux16(a=rout, b=incout, sel=inc, out=m0);
    // if no load, carry rout again to next one
    Mux16(a=m0, b=in, sel=load, out=m1);
    // if no reset, carrout rout as final mux to register 
    Mux16(a=m1, b=false, sel=reset, out=fout);
    // load the final mux into register for next
    // clock cycle
    Register(in=fout, load=true, out=out, out=rout);
}
