// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // instruction[15] is out op-code
    // instruction[12] is a in comp sequence
    
    // A Instruction
    // op-code = 0, if notop, a instruction
    Not(in=instruction[15], out=notop);
    // M1
    // If it's an "a-instruction"
    // put the instruction in the "A register"
    Mux16(a=aluout, b=instruction, sel=notop, out=m1out);
    // instruciton[5]
    // does instruction pertain to A instruction
    Or(a=notop, b=instruction[5], out=ainstruction);
    // A Register
    // output addressM as address in data memory (of M)
    ARegister(in=m1out, load=ainstruction, out=aregister, out[0..14]=addressM);

    // M2
    // Multiplexor for inM and A Register
    // DO we use contents of A register or inM for ALU data
    // beginning of comp sequence, "a" bit.
    // out = "a or m"
    And(a=instruction[15], b=instruction[12], out=am);
    Mux16(a=aregister, b=inM, sel=am, out=aorm); // Goes to ALU
    // Other Data to operatoe on comes from 
    // D Register
    // look at dest codes in language spec
    And(a=instruction[15], b=instruction[4], out=loadd);
    DRegister(in=aluout, load=loadd, out=dregister);

    // The ALU operating on the "selected"  data
    // use all of the "comp" bits for the alu
    ALU(
      x=dregister,
      y=aorm,
      zx=instruction[11],
      nx=instruction[10],
      zy=instruction[9],
      ny=instruction[8],
      f=instruction[7],
      no=instruction[6],
      out=aluout,
      out=outM, // result of computation
      zr=zr, // 1 if (out == 0)
      ng=ng // 1 if (out < 0)
    );

    // Do we write to memory here
    And(a=instruction[3], b=instruction[15], out=writeM);

    // Program Counter here,
    // reset can just be fed into resultant PC after we determine state
    // of instruction
    // jmp only occurs when first three bits != 000
    //
    // need to check greater than, less than, or equal to
    
    // Less than or equal to 0
    And(a=instruction[1], b=zr, out=jeq); // JEQ (out == 0)
    And(a=instruction[2], b=ng, out=jlt); // JLT (out < 0)

    // Greater than 0
    Or(a=ng, b=zr, out=lte); // out <= 0
    Not(in=lte, out=gt); // out > 0

    And(a=instruction[0], b=gt, out=jgt); // JGT (out > 0)

    Or(a=jeq, b=jlt, out=jle); // JLE <= 0
    Or(a=jle, b=jgt, out=jmp);
    // jmp
    // inc / load
    And(a=instruction[15], b=jmp, out=loadpc);
    Not(in=loadpc, out=inc);
    PC(in=aregister, load=loadpc, inc=inc, reset=reset, out[0..14]=pc);
}
